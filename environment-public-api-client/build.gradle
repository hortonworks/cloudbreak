buildscript {
  repositories {
    mavenCentral()
    maven { url = "$repoUrl" }
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}"
    classpath "org.openapitools:openapi-generator-gradle-plugin:$openapiGeneratorGradlePlugin"
  }
}

plugins {
  id 'java'
  id 'org.openapi.generator' version "$openapiGeneratorGradlePlugin"
}

repositories {
  mavenCentral()
}

dependencies {
}

openApiGenerate {
  generatorName = "java"
  library = "jersey3"
  inputSpec = "$projectDir/src/main/yaml/environments2.yaml"
  outputDir = "$projectDir/src/generated"
  modelPackage = "com.cloudera.cdp.environments2.model"
  globalProperties.set([
          apis: "false",
          apiDocs: "false",
          apiTests: "false",
          verbose: "true",
          generateSupportingFiles: "false",
          modelDocs: "false",
          modelTests: "false"
  ])
  configOptions.set([
          dateLibrary: "java8"
  ])
  additionalProperties.set([
          hideGenerationTimestamp: "true"
  ])
}

dependencies {
  implementation project(':common')
  implementation project(':auth-connector')
  implementation project(":authorization-common-api")
  implementation project(":structuredevent-api-cdp")
  implementation project(":structuredevent-model")

  implementation     group: 'org.glassfish.jersey.media',  name: 'jersey-media-multipart',         version: jerseyCoreVersion
  implementation     group: 'org.openapitools',            name: 'jackson-databind-nullable',      version: "$jacksonDatabindNullable"
  implementation     group: 'javax.annotation',            name: 'javax.annotation-api',           version: javaxAnnotationApiVersion
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
  main {
    java.srcDirs += "${projectDir}/src/generated/src/main/java"
  }
}