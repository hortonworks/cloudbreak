import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        maven { url = "$repoUrl" }
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}"
    }
}

plugins {
    id "org.sonarqube" version "${sonarVersion}"
    id 'maven-publish'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: org.sonarqube.gradle.SonarQubePlugin


sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectName", "cb-autoscale-service"
        property "sonar.projectKey", "cb-autoscale-service"
        property "sonar.java.coveragePlugin", "plugin"
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.java.binaries", "build/classes"
        property "sonar.java.libraries", "build/libs/*.jar"
        property "project.reporting.outputEncoding", "UTF-8"
        property "sonar.host.url", "$System.env.SONAR_URL"
        property "sonar.login", "$System.env.SONAR_PASSWORD"
        property "org.gradle.internal.http.socketTimeout", "600000"
        property "org.gradle.internal.http.connectionTimeout", "600000"
        property "sonar.coverage.exclusions", "**/proto/**,**/generated/**"
        property "sonar.exclusions", "**/proto/**,**/generated/**"
    }
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'spring.config.location', System.properties['spring.config.location']
    systemProperty 'spring.config.additional-location', System.properties['spring.config.additional-location']
    if (project.hasProperty('jvmArgs')) {
        jvmArgs += project.jvmArgs.split("\\s+").toList()
    }
}

bootRun {
    main = 'com.sequenceiq.periscope.PeriscopeApplication'
}

bootJar {
    baseName = 'periscope'
    archiveName = 'periscope.jar'
    manifest {
        def grgit = Grgit.open(currentDir: project.projectDir)
        attributes "Implementation-Version": grgit.describe()
    }
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
            artifactId "periscope"
        }
    }
    repositories {
        maven {
            url = "$System.env.NEXUS_URL"
            allowInsecureProtocol = true
            credentials {
                username = "$System.env.NEXUS_USER"
                password = "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}

configurations {
    implementation {
        resolutionStrategy {
            exclude group: 'org.testng'
        }
    }
}

project.tasks.named("bootJar") {
    duplicatesStrategy = 'include'
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'org.springframework.boot', version: springBootVersion) {
            entry 'spring-boot-starter'
            entry ('spring-boot-starter-web') {
                exclude group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
            }
            entry ('spring-boot-starter-actuator') {
                exclude group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
            }
            entry 'spring-boot-starter-data-jpa'
            entry 'spring-boot-starter-aop'
            entry 'spring-boot-starter-freemarker'
            entry 'spring-boot-starter-security'
            entry ('spring-boot-starter-jersey') {
                exclude group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
            }
            entry 'spring-boot-starter-test'
            entry 'spring-boot-starter-freemarker'
        }

        dependencySet(group: 'io.springfox', version: swaggerSpringFoxVersion) {
            entry 'springfox-boot-starter'
        }

        dependencySet (group: 'org.powermock', version: powermockVersion) {
            entry 'powermock-module-junit4'
            entry('powermock-api-mockito2') {
                exclude group: 'org.hamcrest', name: '*'
            }
        }
        dependency group: 'xerces',              name: 'xercesImpl',            version: xerces

        dependency (group: 'io.swagger',                    name: 'swagger-jersey2-jaxrs',          version: swaggerVersion) {
          exclude 'org.yaml:snakeyaml'
        }
        dependency group: 'com.fasterxml.jackson.core',    name: 'jackson-databind',               version: jacksonVersion
        dependency group: 'javax.mail',                    name: 'mail',                           version: '1.5.0-b01'
        dependency group: 'dnsjava',                       name: 'dnsjava',                        version: '2.1.7'
        dependency group: 'javax.xml.bind',                name: 'jaxb-api',                       version: '2.3.0'
        dependency group: 'com.sun.xml.bind',              name: 'jaxb-impl',                      version: '2.3.0'
        dependency group: 'org.glassfish.jaxb',            name: 'jaxb-runtime',                   version: '2.3.0'
        dependency group: 'org.springframework',           name: 'spring-context-support',         version: springFrameworkVersion
        dependency group: 'com.zaxxer',                    name: 'HikariCP',                       version: '3.2.0'

        dependency group: 'com.google.code.gson',               name: 'gson',                      version: gsonVersion
        dependency group: 'org.apache.hadoop',                  name: 'hadoop-yarn-api',           version: hadoopVersion
        dependency group: 'org.freemarker',                     name: 'freemarker',                version: freemarkerVersion
        dependency (group: 'org.quartz-scheduler',               name: 'quartz',                    version: '2.3.2') {
            exclude group: 'com.zaxxer', name: 'HikariCP-java7'
        }
        dependency group: 'org.postgresql',                     name: 'postgresql',                version: postgreSQLVersion
        dependency group: 'org.mybatis',                        name: 'mybatis-migrations',        version: mybatisMigrationVersion
        dependency group: 'org.mockito',                        name: 'mockito-core',              version: mockitoVersion
        dependency group: 'com.openpojo',                       name: 'openpojo',                  version: openPojoVersion
        dependency group: 'com.sequenceiq',                     name: 'consul-api',                version: '1.10'
    }
}

dependencies {
    implementation group: 'com.squareup.okhttp3',               name: 'okhttp',                     version: okhttpVersion
    implementation group: 'com.fasterxml.jackson.core',         name: 'jackson-databind'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-freemarker'
    implementation group: 'org.springframework.boot',           name: 'spring-boot-starter-jersey'

    implementation group: 'com.google.code.gson',          name: 'gson'
    implementation group: 'org.apache.hadoop',             name: 'hadoop-yarn-api'
    implementation group: 'org.freemarker',                name: 'freemarker'
    implementation group: 'org.postgresql',                name: 'postgresql'
    implementation group: 'org.mybatis',                   name: 'mybatis-migrations'
    implementation group: 'org.quartz-scheduler',          name: 'quartz'
    implementation group: 'com.zaxxer',                    name: 'HikariCP'
    implementation group: 'javax.mail',                    name: 'mail'
    implementation group: 'dnsjava',                       name: 'dnsjava'
    implementation group: 'net.sf.ehcache',                name: 'ehcache'

    implementation group: 'io.springfox',                  name: 'springfox-boot-starter'

    implementation group: 'io.opentracing.contrib',    name: 'opentracing-spring-jaeger-starter',        version: opentracingSpringJaegerStarterVersion

    implementation     ('io.swagger:swagger-jersey2-jaxrs') {
      exclude group: 'org.yaml'
    }

    implementation group: 'com.sequenceiq',                name: 'consul-api'

    runtimeOnly group: 'javax.xml.bind',                name: 'jaxb-api'
    runtimeOnly group: 'com.sun.xml.bind',              name: 'jaxb-impl'
    runtimeOnly group: 'org.glassfish.jaxb',            name: 'jaxb-runtime'

    testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test'
    testImplementation group: 'org.mockito',               name: 'mockito-core'
    testImplementation group: 'org.powermock',             name: 'powermock-module-junit4'
    testImplementation group: 'org.powermock',             name: 'powermock-api-mockito2'
    testImplementation group: 'com.openpojo',              name: 'openpojo'
    testImplementation group: "com.h2database",            name: "h2",     version: h2databaseVersion
    testImplementation project(path: ':common', configuration: 'tests')
    testImplementation project(path: ':authorization-common', configuration: 'tests')

    implementation project(':core-api')
    implementation project(':autoscale-api')
    implementation project(':freeipa-api')
    implementation project(':common')
    implementation project(':workspace')
    implementation project(':secret-engine')
    implementation project(':client-cm')
    implementation project(':authorization-common')
    implementation project(':usage-collection')
    implementation(project(":audit-connector")) {
         transitive = false
    }
}

test {
    include 'com/sequenceiq/**'
    testLogging.showStandardStreams = true
    ignoreFailures = false
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message)
    }
}
