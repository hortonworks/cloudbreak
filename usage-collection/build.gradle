buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "$repoUrl" }
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
  }
}

plugins {
  id 'de.undercouch.download' version '3.4.0'
}

apply plugin: 'com.google.protobuf'

dependencies {
  compile project(':common')

  compile ("io.grpc:grpc-all:1.22.0") {
    exclude group: 'org.mockito'
  }
  testCompile group: 'org.mockito',                     name: 'mockito-core', version: mockitoVersion
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

checkstyle {
  checkstyleMain.exclude '**/generated/**'
}

task ensureDirectory {
  doLast {
    mkdir "src/main/proto"
  }
}

task downloadFile(type: Download) {
  src 'https://github.infra.cloudera.com/raw/thunderhead/thunderhead/1c52370c58cf40e3606f29116a018f8cfbbf4330/services/libs/protocols/src/main/proto/usage.proto'
  dest 'src/main/proto'
  overwrite true
}

sourceSets {
  main {
    proto {
      'src/main/proto'
    }
  }
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.6.1"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.12.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}

afterEvaluate {
  extractIncludeProto.dependsOn downloadFile
  downloadFile.dependsOn ensureDirectory
}
