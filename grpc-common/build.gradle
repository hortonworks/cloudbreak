apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    mavenCentral()
    maven { url = "$repoUrl" }
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
  }
}

dependencies {
  compile project(':common')

  compile        group: 'io.opentracing.contrib',       name: 'opentracing-grpc',     version: opentracingGrpcVersion
  implementation group: 'com.squareup.okhttp',          name: 'okhttp',               version: '2.7.4'
  implementation group: 'org.slf4j',                    name: 'slf4j-api',            version: slf4jApiVersion

  implementation ("io.grpc:grpc-all:1.22.0") {
    exclude group: 'org.mockito'
    exclude group: 'junit', module: 'junit'
    exclude group: 'com.squareup.okhttp'
    exclude group: 'org.hamcrest'
    exclude group: 'io.grpc', module: 'grpc-testing'
  }

  testImplementation (group: 'org.mockito',  name: 'mockito-core', version: mockitoVersion) {
    exclude group: 'org.hamcrest'
  }
}

checkstyle {
  checkstyleMain.exclude '**/generated/**'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.6.1"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.12.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}
