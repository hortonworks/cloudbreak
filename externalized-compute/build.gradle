import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        maven { url = "$repoUrl" }
        maven { url = "$springRepoUrl" }
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}"
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "maven-publish"
    id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
    id "org.springframework.boot" version "${springBootVersion}"
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

dependencyManagement {
  dependencies {
    dependency group: 'org.apache.tomcat.embed',  name: 'tomcat-embed-core',  version: tomcatVersion
    dependency group: 'org.springframework',      name: 'spring-webmvc',      version: springFrameworkVersion
    dependency group: 'io.netty',                 name: 'netty-codec',        version: nettyVersion
  }
}

dependencies {
    implementation project(':common')
    implementation project(':grpc-common')
    implementation project(':authorization-common')
    implementation project(':auth-connector')
    implementation project(':flow')
    implementation project(':flow-api')
    implementation project(':externalized-compute-api')
    implementation project(':environment-api')
    implementation project(':template-manager-tag')
    implementation project(':service-common')
    implementation project(':liftie-connector')

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jersey', version: springBootVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: springBootVersion
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: jerseyCoreVersion
    implementation group: 'org.glassfish.jersey.ext', name: 'jersey-micrometer', version: jerseyMicrometerVersion
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion
    implementation group: 'com.dyngr', name: 'polling', version: dyngrPollingVersion
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2-jakarta', version: swaggerCoreVersion
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: javaxXmlBindJaxbApiVersion
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: javaxAnnotationApiVersion
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsLangVersion

    testImplementation project(':cloud-api')
    testImplementation project(path: ':authorization-common', configuration: 'tests')
    testImplementation project(path: ':flow', configuration: 'tests')
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: mockitoVersion
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'spring.config.location', System.properties['spring.config.location']
    if (project.hasProperty('jvmArgs')) {
        jvmArgs += project.jvmArgs.split("\\s+").toList()
    }
}

bootRun {
    mainClass = 'com.sequenceiq.externalizedcompute.ExternalizedComputeClusterApplication'
}

bootJar {
    archiveBaseName = 'externalizedcompute'
    archiveFileName = 'externalizedcompute.jar'
    manifest {
        def grgit = Grgit.open(currentDir: project.projectDir)
        attributes "Implementation-Version": grgit.describe()
    }
    //TODO  This could be removed if commercial side will be also on chainguard images
    requiresUnpack '**/bc*.jar'
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
    repositories {
        maven {
            url = "$System.env.NEXUS_URL"
            allowInsecureProtocol = true
            credentials {
                username = "$System.env.NEXUS_USER"
                password = "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}