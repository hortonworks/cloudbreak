buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

jar {
  baseName = 'cloudbreak-integration-test'
}

apply plugin: 'org.springframework.boot'

dependencies {
  compile project(':core-api')
  compile (project(':orchestrator-api')) {
    transitive = false;
  }
  compile (project(':orchestrator-salt')) {
    transitive = false;
  }
  compile (project(':cloud-api')) {
    transitive = false;
  }
  compile (project(':autoscale-api')) {
    transitive = false;
  }
  compile("com.sequenceiq:${ambariClientName}:${ambariClientVersion}") {
    exclude group: 'org.slf4j';
  }
  compile (group: 'com.amazonaws',                name: 'aws-java-sdk',                   version: awsSdkVersion) {
    exclude group: 'commons-logging'
  }
  compile (group: 'com.microsoft.azure',          name: 'azure',                          version: azureSdkVersion) { exclude group: 'org.slf4j' }
  compile group:  'com.google.apis',              name: 'google-api-services-compute',    version: 'v1-rev133-1.22.0'
  compile group: 'com.google.apis',               name: 'google-api-services-storage',    version: 'v1-rev94-1.22.0'
  compile group: 'com.microsoft.azure',           name: 'azure-storage',                  version: '5.0.0'
  compile group: 'com.microsoft.azure',           name: 'azure-data-lake-store-sdk',      version: '2.1.5'
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter',            version: springBootVersion
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter-test',       version: springBootVersion
  compile group:  'org.springframework',          name: 'spring-web',                     version: springFrameworkVersion
  compile group:  'org.springframework',          name: 'spring-context-support',         version: springFrameworkVersion
  compile group:  'org.apache.velocity',          name: 'velocity',                       version: '1.7'
  compile group:  'com.hierynomus',               name: 'sshj',                           version: '0.15.0'
  compile group:  'org.testng',                   name: 'testng',                         version: '6.9.10'
  compile group:  'javax.el',                     name: 'javax.el-api',                   version: '3.0.0'
  compile group:  'org.apache.commons',           name: 'commons-lang3',                  version: '3.4'
  compile group:  'org.pacesys',                  name: 'openstack4j-core',               version: '3.0.4-guava-update'
  compile (group: 'org.pacesys.openstack4j.connectors', name: 'openstack4j-jersey2',            version: '3.0.4') {
    exclude group: 'org.pacesys', module: 'openstack4j-core'
  }
  compile(group:  'org.apache.sshd',              name: 'sshd-core',                      version: '1.2.0') {
    exclude group: 'ch.qos.logback'
  }
  compile group: 'com.sequenceiq',                name: 'consul-api',                     version: '1.10'
  compile group: 'com.sparkjava',                 name: 'spark-core',                     version: '2.5'

  compile ("org.uncommons:reportng:1.1.4") {
    exclude group: "velocity"
  }
  compile group: 'com.google.code.findbugs',      name: 'annotations',                    version: '3.0.1'
  runtime group: 'com.google.inject',             name: 'guice',                          version: '3.0'
  runtime group: 'org.hibernate',                 name: 'hibernate-validator',            version: hibernateValidatorVersion
}

task buildInfo(type: BuildInfoTask) {
  destination = file("$buildDir")
  applicationPropertiesPath = "$buildDir"
  basename = jar.baseName
  buildVersion = version
}

compileJava.dependsOn buildInfo

class BuildInfoTask extends DefaultTask {
  File destination = new File("build.info")
  String applicationPropertiesPath
  String basename
  String buildVersion

  @TaskAction
  def writeBuildInfo() {
    destination.mkdirs()
    File applicationDestination = new File(applicationPropertiesPath + "/resources/main")
    applicationDestination.mkdirs()
    new File(destination, "build.info").withWriter { out ->
      [
              "ARTIFACT_BASENAME=" + basename,
              "ARTIFACT_VERSION=" + buildVersion,
      ].each { out.println it }
    }
    new File(applicationDestination, "application.properties").withWriter { out ->
      [
              "info.app.name=" + basename,
              "info.app.version=" + buildVersion
      ].each { out.println it }
    }
  }
}