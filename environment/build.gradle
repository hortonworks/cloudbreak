import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        maven { url = "$repoUrl" }
        maven { url = "$springRepoUrl" }
        mavenCentral()
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}"
        classpath "org.openapitools:openapi-generator-gradle-plugin:$openapiGeneratorGradlePlugin"
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'maven-publish'
    id 'org.springframework.boot' version "${springBootVersion}"
    id "org.sonarqube" version "${sonarVersion}"
    id 'org.openapi.generator' version "$openapiGeneratorGradlePlugin"
}

apply plugin: "org.sonarqube"
apply plugin: 'io.spring.dependency-management'

configurations.all {
    resolutionStrategy {
        force "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
    }
}

sonar {
    properties {
        property("sonar.projectName", "Environment_Service_UT_master")
        property("sonar.projectKey", "Environment_Service_UT_master")
        property("sonar.java.coveragePlugin", "plugin")
        property("sonar.sourceEncoding", "UTF-8")
        property("sonar.sources", "src/main")
        property("sonar.tests", "src/test")
        property("sonar.java.binaries", "build/classes")
        property("sonar.project.reporting.outputEncoding", "UTF-8")
        property("sonar.host.url", "$System.env.SONAR_URL")
        property("sonar.token", "$System.env.SONAR_TOKEN")
        property("org.gradle.internal.http.socketTimeout", "600000")
        property("org.gradle.internal.http.connectionTimeout", "600000")
        property("sonar.coverage.exclusions", "**/proto/**,**/generated/**")
        property("sonar.exclusions", "**/proto/**,**/generated/**")
    }
}

dependencyManagement {
  dependencies {
    dependency group: 'io.projectreactor.netty',  name: 'reactor-netty-http',  version: reactorNettyHttp
  }
}

dependencies {
    implementation     project(':audit-connector')
    implementation     project(":authorization-common")
    implementation     project(":common")
    implementation     project(':core-common')
    implementation     project(":secret-engine")
    implementation     project(":flow")
    implementation     project(":auth-connector")
    implementation     project(":environment-api")
    implementation     project(":environment-common")
    implementation     project(":notification-sender")
    implementation     project(":cloud-reactor")
    implementation     project(":cloud-reactor-api")
    implementation     project(":datalake-api")
    implementation     project(":core-api")
    implementation     project(":cloud-aws-cloudformation")
    implementation     project(":cloud-aws-common")
    implementation     project(":cloud-azure")
    implementation     project(":cloud-mock")
    implementation     project(":cloud-yarn")
    implementation     project(":cloud-gcp")
    implementation     project(":idbmms-connector")
    implementation     project(":redbeams-api")
    implementation     project(":freeipa-api")
    implementation     project(":externalized-compute-api")
    implementation     project(":structuredevent-service-cdp")
    implementation     project(":status-checker")
    implementation     project(":template-manager-tag")
    implementation     project(':cluster-dns-connector')
    implementation     project(":sdx-connector")
    implementation     project(':usage-collection')
    implementation     project(':wiam-connector')
    implementation     project(':service-common')

    implementation     group: "org.yaml",                  name: "snakeyaml",                                version: snakeYamlVersion
    implementation     group: "com.google.code.gson",      name: "gson",                                     version: gsonVersion
    implementation     group: 'io.swagger.core.v3',        name: 'swagger-jaxrs2-jakarta',                   version: swaggerCoreVersion
    implementation     group: 'javax.xml.bind',            name: 'jaxb-api',                                 version: javaxXmlBindJaxbApiVersion
    implementation     group: "org.glassfish.jersey.core", name: "jersey-server",                            version: jerseyCoreVersion
    implementation     group: "org.mybatis",               name: "mybatis-migrations",                       version: mybatisMigrationVersion
    implementation     group: "org.postgresql",            name: "postgresql",                               version: postgreSQLVersion
    implementation     group: "org.springframework.boot",  name: "spring-boot-starter",                      version: springBootVersion
    implementation     group: "org.springframework.boot",  name: "spring-boot-starter-web",                  version: springBootVersion
    implementation     group: "org.springframework.boot",  name: "spring-boot-starter-actuator",             version: springBootVersion
    implementation     group: "org.springframework.boot",  name: "spring-boot-starter-jersey",               version: springBootVersion
    implementation     group: "org.springframework.boot",  name: "spring-boot-starter-data-jpa",             version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-quartz',               version: springBootVersion
    implementation     group: 'com.dyngr',                 name: 'polling',                                  version: dyngrPollingVersion
    implementation     group: 'net.sourceforge.jregex',    name: 'jregex',                                   version: '1.2_01'
    implementation     group: 'com.fasterxml.jackson.core',name: 'jackson-databind',                         version: jacksonVersion
    implementation     group: 'com.squareup.okhttp3',      name: 'okhttp',                                   version: okhttpVersion
    implementation     group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart',                  version: jerseyCoreVersion
    implementation     group: 'org.glassfish.jersey.ext',  name: 'jersey-micrometer',                        version: jerseyMicrometerVersion
    implementation     group: 'org.openapitools',          name: 'jackson-databind-nullable',                version: "$jacksonDatabindNullable"

    implementation(group: 'net.minidev', name: 'json-smart') {
      version {
        strictly jsonSmartVersion
      }
      because 'CVE-2021-31684'
    }
    implementation(group: 'net.minidev', name: 'accessors-smart') {
      version {
        strictly accessorSmartVersion
      }
      because 'because there is no jar for 2.4.3 in maven central repo...'
    }

    testImplementation project(path: ':authorization-common', configuration: 'tests')
    testImplementation project(path: ':common', configuration: 'tests')
    testImplementation project(path: ':flow', configuration: 'tests')
    testImplementation group: "org.mockito",               name: "mockito-core",                             version: mockitoVersion
    testImplementation(group: "org.springframework.boot",  name: "spring-boot-starter-test",                 version: springBootVersion) {
        exclude group: "junit"
    }

    testImplementation group: "com.openpojo",              name: "openpojo",                                 version: openPojoVersion
    testImplementation group: "com.h2database",            name: "h2",                                       version: h2databaseVersion
    testImplementation group: "org.mockito",               name: "mockito-core",                             version: mockitoVersion
    testImplementation group: "com.google.guava",          name: "guava",                                    version: guavaVersion
    testImplementation group: 'org.assertj',               name: 'assertj-core',                             version: assertjVersion

    testRuntimeOnly group: 'org.testcontainers',      name: 'postgresql',                               version: testContainersVersion
    testRuntimeOnly group: 'org.awaitility',          name: 'awaitility',                               version: awaitilityVersion
}

bootRun {
  systemProperty "spring.profiles.active", System.properties["spring.profiles.active"]
  systemProperty "spring.config.location", System.properties["spring.config.location"]
  systemProperty 'spring.config.additional-location', System.properties['spring.config.additional-location']

  environment "AWS_ACCESS_KEY_ID", System.getenv('AWS_ACCESS_KEY_ID')
  environment "AWS_SECRET_ACCESS_KEY", System.getenv('AWS_SECRET_ACCESS_KEY')
  environment "CB_AWS_ACCOUNT_ID", System.getenv('CB_AWS_ACCOUNT_ID')
  environment "AWS_GOV_ACCESS_KEY_ID", System.getenv('AWS_GOV_ACCESS_KEY_ID')
  environment "AWS_GOV_SECRET_ACCESS_KEY", System.getenv('AWS_GOV_SECRET_ACCESS_KEY')
  environment "CB_AWS_GOV_ACCOUNT_ID", System.getenv('CB_AWS_GOV_ACCOUNT_ID')
  //Probably will need to add more for AZURE, GCE, etc

  if (project.hasProperty("jvmArgs")) {
    jvmArgs += project.jvmArgs.split("\\s+").toList()
  }
}

bootRun {
  mainClass = "com.sequenceiq.environment.EnvironmentApplication"
}

bootJar {
  archiveBaseName = "environment"
  archiveFileName= "environment.jar"
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
  //TODO  This could be removed if commercial side will be also on chainguard images
  requiresUnpack '**/bc*.jar'
}

publishing {
  publications {
    bootJava(MavenPublication) {
      artifact bootJar
    }
  }
  repositories {
    maven {
      url = "$System.env.NEXUS_URL"
      allowInsecureProtocol = true
      credentials {
        username = "$System.env.NEXUS_USER"
        password = "$System.env.NEXUS_PASSWORD"
      }
    }
  }
}

openApiGenerate {
  generatorName = "java"
  library = "jersey3"
  inputSpec = "$projectDir/src/main/resources/compute-api.yaml"
  outputDir = "$projectDir/src/generated"
  apiPackage = "com.cloudera.thunderheadcompute.api"
  modelPackage = "com.cloudera.thunderheadcompute.model"
  invokerPackage = "com.sequenceiq.thunderheadcompute"
  configOptions = [
          dateLibrary: "java8"
  ]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
  main {
    java.srcDirs += "${projectDir}/src/generated/src/main/java"
  }
}
