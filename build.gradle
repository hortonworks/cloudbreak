buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE")
    }
}

def env = project.hasProperty('env') ? project.getProperty('env') : 'local'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'sonar-runner'

sourceCompatibility = 1.7
group = 'com.sequenceiq'

allprojects {
    apply from: "$rootDir/gradle/versioning.gradle"
    ext.config = new ConfigSlurper(env).parse(file("$rootDir/gradle/config/buildConfig.groovy").toURL())
}

jar {
    baseName = 'cloudbreak'
}

configurations {
    deployerJars
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "s3://seq-repo/releases") {
                authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
            }
            snapshotRepository(url: "s3://seq-repo/snapshots") {
                authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
            }
        }
    }
}

checkstyleMain {
    ignoreFailures = false
    configProperties = ['config_loc': "$projectDir/config/checkstyle"]
}

checkstyleTest {
    ignoreFailures = false
    configProperties = ['config_loc': "$projectDir/config/checkstyle"]
}

jacoco {
    toolVersion = "0.6.2.201302030002"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

sonarRunner {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
    sonarProperties {
        property "sonar.host.url", "$config.sonar_host_url"
        property "sonar.jdbc.url", "$config.sonar_jdbc_url"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "$System.env.SONAR_USERNAME"
        property "sonar.jdbc.password", "$System.env.SONAR_PASSWORD"
        property "project.reporting.outputEncoding", "UTF-8"
        property "sonar.projectKey", "cloudbreak"
        property "sonar.projectName", "cloudbreak"
    }
}

repositories {
    mavenCentral()
    maven { url "http://seq-repo.s3-website-eu-west-1.amazonaws.com/releases" }
    maven { url "http://seq-repo.s3-website-eu-west-1.amazonaws.com/snapshots" }
    maven { url "http://maven.sequenceiq.com/snapshots" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.0.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-actuator:1.0.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.0.2.RELEASE'
    compile 'org.springframework.security:spring-security-config:3.2.3.RELEASE'
    compile 'org.springframework.security:spring-security-web:3.2.3.RELEASE'
    compile 'com.h2database:h2:1.3.175'
    compile 'com.amazonaws:aws-java-sdk:1.7.8.1'
    compile 'org.hibernate:hibernate-validator:5.1.1.Final'
    compile 'com.sequenceiq.azure:azure-client:1.0-SNAPSHOT'
    compile ('com.sequenceiq.ambari:ambari-client:1.0-SNAPSHOT'){
        exclude group: "org.slf4j";
    }
    compile 'org.codehaus.jettison:jettison:1.3.5'
    compile 'org.codehaus.groovy:groovy-all:1.8.6'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.guava:guava:16.0.1'
    compile 'com.thoughtworks.xstream:xstream:1.4.7'
    compile("org.codehaus.groovy.modules.http-builder:http-builder:0.7.1") {
        exclude group: "commons-logging";
        exclude group: "xml-apis";
        exclude group: "groovy"
    }

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.0.2.RELEASE'
    deployerJars 'org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE'
}

task wrapper(type: Wrapper) { gradleVersion = "1.12" }

task buildInfo(type: BuildInfoTask) {
    destination = file("$buildDir")
    basename = jar.baseName
    buildVersion =  version
}

compileJava.dependsOn buildInfo

class BuildInfoTask extends DefaultTask {
    File destination = new File("build.info")
    String basename
    String buildVersion

    @TaskAction
    def writeBuildInfo() {
        destination.mkdirs()
        new File(destination, "build.info").withWriter { out ->
            [
                "ARTIFACT_BASENAME=" + basename,
                "ARTIFACT_VERSION=" + buildVersion,
            ].each { out.println it } }
    }
}
