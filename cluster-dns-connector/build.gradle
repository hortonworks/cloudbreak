apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    mavenCentral()
    maven { url = "$repoUrl" }
    maven { url = "$springRepoUrl" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}"
  }
}

apply plugin: 'io.spring.dependency-management'

dependencyManagement {
  dependencies {
    dependencySet(group: 'org.springframework.boot', version: springBootVersion) {
      entry 'spring-boot-starter-test'
    }
  }
}

dependencies {
  implementation project(':grpc-common')
  implementation project(':auth-connector')

  implementation     group: 'com.dyngr',                 name: 'polling',                         version: dyngrPollingVersion

  implementation     group: 'javax.annotation',                    name: 'javax.annotation-api',           version: javaxAnnotationApiVersion
  implementation     group: 'com.google.protobuf',                 name: 'protobuf-java',                  version: protobufVersion
  implementation     group: 'org.bouncycastle',                    name: 'bcpkix-fips',                    version: bouncycastlePkixFipsVersion
  implementation ("io.grpc:grpc-all:$grpcVersion") {
    exclude group: 'org.mockito'
  }

  testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

checkstyle {
  checkstyleMain.exclude '**/generated/**'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc { option '@generated=omit' }
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}

configurations.configureEach {
  resolutionStrategy {
    force "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
  }
}
