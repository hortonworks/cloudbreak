import org.ajoberstar.grgit.Grgit

buildscript {
  repositories {
    maven { url = "$repoUrl" }
    maven { url = "$springRepoUrl" }
    mavenCentral()
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}"
  }
}

plugins {
  id "java"
  id "checkstyle"
  id "jacoco"
  id "maven-publish"
  id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
  id "org.springframework.boot" version "${springBootVersion}"
  id "org.sonarqube" version "${sonarVersion}"
}

apply plugin: "org.sonarqube"

sonar {
  properties {
    property("sonar.projectName", "Freeipa_Service_UT_master")
    property("sonar.projectKey", "Freeipa_Service_UT_master")
    property("sonar.java.coveragePlugin", "plugin")
    property("sonar.sourceEncoding", "UTF-8")
    property("sonar.sources", "src/main")
    property("sonar.tests", "src/test")
    property("sonar.java.binaries", "build/classes")
    property("sonar.project.reporting.outputEncoding", "UTF-8")
    property("sonar.host.url", "$System.env.SONAR_URL")
    property("sonar.token", "$System.env.SONAR_TOKEN")
    property("org.gradle.internal.http.socketTimeout", "600000")
    property("org.gradle.internal.http.connectionTimeout", "600000")
    property("sonar.coverage.exclusions", "**/proto/**,**/generated/**")
    property("sonar.exclusions", "**/proto/**,**/generated/**")
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.apache.tomcat.embed',  name: 'tomcat-embed-core',  version: tomcatVersion
    dependency group: 'org.springframework',      name: 'spring-webmvc',      version: springFrameworkVersion
    dependency group: 'io.projectreactor.netty',  name: 'reactor-netty-http', version: reactorNettyHttp
  }
}

dependencies {
  implementation     group: 'io.swagger.core.v3',        name: 'swagger-jaxrs2-jakarta',               version: swaggerCoreVersion
  implementation     group: 'javax.xml.bind',            name: 'jaxb-api',                             version: javaxXmlBindJaxbApiVersion
  implementation     group: 'com.squareup.okhttp3',      name: 'okhttp',                               version: okhttpVersion

  implementation     group: 'com.google.code.gson',      name: 'gson',                                 version: gsonVersion
  // apacheds-kerberos-codec using a pretty old mina-core and that library was not update since 2020
  implementation     group: 'org.apache.mina',           name: 'mina-core',                            version: '2.2.5'
  implementation     group: 'javax.activation',          name: 'activation',                           version: '1.1.1'
  implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server',                        version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.media',name: 'jersey-media-json-jackson',            version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.ext',  name: 'jersey-micrometer',                    version: jerseyMicrometerVersion
  implementation     group: 'org.mybatis',               name: 'mybatis-migrations',                   version: mybatisMigrationVersion
  implementation     group: 'org.postgresql',            name: 'postgresql',                           version: postgreSQLVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter',                  version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web',              version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator',         version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey',           version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa',         version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-quartz',           version: springBootVersion
  implementation     group: 'org.springframework',       name: 'spring-context-support',               version: springFrameworkVersion
  implementation     group: 'org.hibernate',             name: 'hibernate-envers',                     version: hibernateCoreVersion
  implementation    (group: 'org.springframework.data',  name: 'spring-data-envers',                   version: springDataJpaFrameworkVersion) {
    exclude group: 'org.hibernate', module: 'hibernate-envers'
  }
  implementation group: 'org.bouncycastle',                 name: 'bc-fips',                        version: bouncycastleFipsVersion
  implementation     group: 'com.google.guava',          name: 'guava',                                version: guavaVersion
  implementation     (group: 'org.apache.directory.server', name: 'apacheds-kerberos-codec',           version: '2.0.0.AM27') {
    exclude group: 'org.apache.mina', module: 'mina-core'
  }
  implementation     group: 'org.apache.commons',        name: 'commons-lang3',                        version: apacheCommonsLangVersion

  implementation     group: 'com.github.briandilley.jsonrpc4j', name: 'jsonrpc4j',                     version: '1.7'
  implementation     group: 'com.dyngr',                        name: 'polling',                       version: dyngrPollingVersion
  implementation     group: 'org.freemarker',                   name: 'freemarker',                    version: freemarkerVersion
  testImplementation group: 'org.mockito',                      name: 'mockito-inline',                version: mockitoInlineVersion
  testImplementation ('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit'
  }

  // mockito juniper depends on ByteBuddy 1.9.7, but hibernate use older version
  implementation group: 'net.bytebuddy', name: 'byte-buddy', version: byteBuddyVersion

  implementation(group: 'net.minidev', name: 'json-smart') {
    version {
      strictly jsonSmartVersion
    }
    because 'CVE-2021-31684'
  }
  implementation(group: 'net.minidev', name: 'accessors-smart') {
    version {
      strictly accessorSmartVersion
    }
    because 'because there is no jar for 2.4.3 in maven central repo...'
  }

  constraints {
    implementation     group: 'com.google.code.gson',      name: 'gson',                           version: gsonVersion
    implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server',                  version: jerseyCoreVersion
    implementation     group: 'org.glassfish.jersey.ext',  name: 'jersey-micrometer',              version: jerseyMicrometerVersion
    implementation     group: 'org.mybatis',               name: 'mybatis-migrations',             version: mybatisMigrationVersion
    implementation     group: 'org.postgresql',            name: 'postgresql',                     version: postgreSQLVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter',            version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web',        version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator',   version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey',     version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa',   version: springBootVersion
    implementation     group: 'org.hibernate',             name: 'hibernate-envers',               version: hibernateCoreVersion
    testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test',       version: springBootVersion
    testImplementation group: 'org.mockito',               name: 'mockito-inline',                 version: mockitoInlineVersion
  }

  implementation project(':secret-rotation')
  implementation project(':secret-rotation-api')
  implementation project(":structuredevent-service-cdp")
  implementation project(':structuredevent-model')
  implementation project(':audit-connector')
  implementation project(':wiam-connector')
  implementation project(':authorization-common')
  implementation project(':auth-distributor-connector')
  implementation project(':cloud-reactor-api')
  implementation project(':cloud-reactor')
  implementation project(':cloud-aws-common')
  implementation project(':orchestrator-api')
  implementation project(':flow')
  implementation project(':freeipa-api')
  implementation project(':freeipa-client')
  implementation project(':telemetry-common')
  implementation project(':usage-collection')
  implementation project(':metrics-client')
  implementation project(':common')
  implementation project(':core-api')
  implementation project(':databus-connector')
  implementation project(':secret-engine')
  implementation project(':notification-sender')
  implementation project(':environment-api')
  implementation project(':environment-common')
  implementation project(':core-common')
  implementation project(':datalake-api')
  implementation project(':cluster-proxy')
  implementation project(':status-checker')
  implementation project(':template-manager-tag')
  implementation project(':cloud-aws-cloudformation')
  implementation project(':cloud-aws-native')
  implementation project(':cloud-aws-common')
  implementation project(':cloud-aws-gov')
  implementation project(':cloud-azure')
  implementation project(':cloud-gcp')
  implementation project(':cloud-template')
  implementation project(':ccm-connector')
  implementation project(':service-common')
  implementation project(':cluster-dns-connector')
  implementation project(':environment-remote-api')
  implementation project(":environment-public-api-client")

  implementation project(':orchestrator-salt')
  runtimeOnly project(':cloud-mock')
  runtimeOnly project(':cloud-azure')
  runtimeOnly project(':cloud-gcp')

  testImplementation project(path: ':secret-rotation', configuration: 'tests')
  testImplementation project(path: ':authorization-common', configuration: 'tests')
  testImplementation project(path: ':common', configuration: 'tests')
  testImplementation project(path: ':secret-engine', configuration: 'tests')
  testImplementation project(path: ':flow', configuration: 'tests')

  testImplementation group: 'com.hubspot.jinjava',         name: 'jinjava',                        version: jinjavaVersion
  testImplementation group: 'com.fasterxml.jackson.core',  name: 'jackson-databind',               version: jacksonVersion

  testRuntimeOnly group: 'org.testcontainers',        name: 'postgresql',                     version: testContainersVersion
  testRuntimeOnly group: 'org.awaitility',            name: 'awaitility',                     version: awaitilityVersion
}

bootRun {
  systemProperty "spring.profiles.active", System.properties["spring.profiles.active"]
  systemProperty "spring.config.location", System.properties["spring.config.location"]
  systemProperty 'spring.config.additional-location', System.properties['spring.config.additional-location']

  environment "AWS_ACCESS_KEY_ID", System.getenv('AWS_ACCESS_KEY_ID')
  environment "AWS_SECRET_ACCESS_KEY", System.getenv('AWS_SECRET_ACCESS_KEY')
  environment "CB_AWS_ACCOUNT_ID", System.getenv('CB_AWS_ACCOUNT_ID')
  environment "AWS_GOV_ACCESS_KEY_ID", System.getenv('AWS_GOV_ACCESS_KEY_ID')
  environment "AWS_GOV_SECRET_ACCESS_KEY", System.getenv('AWS_GOV_SECRET_ACCESS_KEY')
  environment "CB_AWS_GOV_ACCOUNT_ID", System.getenv('CB_AWS_GOV_ACCOUNT_ID')
  //Probably will need to add more for AZURE, GCE, etc

  if (project.hasProperty("jvmArgs")) {
    jvmArgs += project.jvmArgs.split("\\s+").toList()
  }
}

bootRun {
  mainClass = 'com.sequenceiq.freeipa.FreeIpaApplication'
}

bootJar {
  archiveBaseName = 'freeipa'
  archiveFileName= 'freeipa.jar'
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
  //TODO  This could be removed if commercial side will be also on chainguard images
  requiresUnpack '**/bc*.jar'
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
    repositories {
        maven {
            url = "$System.env.NEXUS_URL"
            allowInsecureProtocol = true
            credentials {
                username = "$System.env.NEXUS_USER"
                password = "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}
