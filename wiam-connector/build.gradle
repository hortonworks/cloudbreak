apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    mavenCentral()
    maven { url = "$repoUrl" }
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}"
  }
}

dependencies {
  implementation group: 'com.google.protobuf',          name: 'protobuf-java',        version: protobufVersion

  implementation project(':grpc-common')

  implementation ("io.grpc:grpc-all:$grpcVersion") {
    exclude group: 'org.mockito'
  }

  testImplementation group: 'org.mockito',                       name: 'mockito-core',                   version: mockitoVersion
  testImplementation group: 'org.hamcrest',                      name: 'hamcrest',                       version: hamcrestVersion
  testImplementation group: "org.assertj",                       name: "assertj-core",                   version: assertjVersion
}

checkstyle {
  checkstyleMain.exclude '**/generated/**'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}
