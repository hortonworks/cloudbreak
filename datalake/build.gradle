import org.ajoberstar.grgit.Grgit

buildscript {
  repositories {
    maven { url = "$repoUrl" }
    maven { url = "$springRepoUrl" }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
  }
}

plugins {
  id "java"
  id "checkstyle"
  id "jacoco"
  id 'maven'
  id "org.springframework.boot" version "2.1.2.RELEASE"
  id "org.sonarqube" version "2.7"
}

apply plugin: org.sonarqube.gradle.SonarQubePlugin

sonarqube {
  properties {
    property "sonar.sourceEncoding", "UTF-8"
    property "sonar.projectName", "cb-datalake-service"
    property "sonar.projectKey", "cb-datalake-service"
    property "sonar.java.coveragePlugin", "plugin"
    property "sonar.sources", "src/main"
    property "sonar.tests", "src/test"
    property "sonar.java.binaries", "build/classes"
    property "sonar.java.libraries", "build/libs/*.jar"
    property "project.reporting.outputEncoding", "UTF-8"
    property "sonar.host.url", "$System.env.SONAR_URL"
    property "sonar.login", "$System.env.SONAR_USERNAME"
    property "sonar.password", "$System.env.SONAR_PASSWORD"
  }
}

dependencies {
  compile project(':authorization-common')
  compile project(':common')
  compile project(':flow')
  compile project(':secret-engine')
  compile project(':notification-sender')
  compile project(':idbmms-connector')
  compile project(':datalake-dr-connector')

  compile project(':core-api')
  compile project(':datalake-api')
  compile project(':environment-api')
  compile project(':redbeams-api')
  compile project(':status-checker')

  implementation     project(":freeipa-api")
  implementation     project(':client-cm')
  implementation     project(":structuredevent-service-cdp")

  implementation     group: 'com.squareup.okhttp3',      name: 'okhttp',                          version: okhttpVersion
  implementation     group: 'com.google.code.gson',      name: 'gson',                            version: '2.6.2'
  implementation     group: 'dnsjava',                   name: 'dnsjava',                         version: '2.1.7'
  implementation     group: 'com.dyngr',                 name: 'polling',                         version: '1.1.3'
  implementation     group: 'io.micrometer',             name: 'micrometer-core',                 version: micrometerVersion
  implementation     group: 'io.micrometer',             name: 'micrometer-jersey2',              version: micrometerVersion
  implementation     group: 'io.micrometer',             name: 'micrometer-registry-prometheus',  version: micrometerVersion
  implementation     group: 'io.springfox',              name: 'springfox-swagger2',              version: swagger2Version
  implementation     group: 'io.springfox',              name: 'springfox-swagger-ui',            version: swagger2Version
  implementation     ("io.swagger:swagger-jersey2-jaxrs:$swaggerVersion") {
    exclude group: 'org.yaml'
  }
  implementation     group: 'javax.xml.bind',            name: 'jaxb-api',                        version: '2.3.1'
  implementation     group: 'org.glassfish.jaxb',        name: 'jaxb-runtime',                    version: '2.3.1'
  implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server',                   version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.media',name: 'jersey-media-json-jackson',       version: jerseyCoreVersion
  implementation     group: 'org.mybatis',               name: 'mybatis-migrations',              version: '3.2.0'
  implementation     group: 'org.postgresql',            name: 'postgresql',                      version: postgreSQLVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter',             version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web',         version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator',    version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey',      version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa',    version: springBootVersion

  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-quartz',      version: springBootVersion

  implementation     group: 'com.cloudera.api.swagger',    name: 'cloudera-manager-api-swagger',  version: cmClientVersion

  testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test',        version: springBootVersion
//  testImplementation group: 'org.mockito',               name: 'mockito-core',                    version: mockitoVersion
  testImplementation group: 'org.mockito',               name: 'mockito-inline',                  version: mockitoVersion


  // mockito juniper depends on ByteBuddy 1.9.7, but hibernate use older version
  implementation group: 'net.bytebuddy',                 name: 'byte-buddy',                      version: '1.9.12'

  testImplementation project(path: ':authorization-common', configuration: 'tests')

  testCompile project(path: ':common', configuration: 'tests')
}

bootRun {
  systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
  systemProperty 'spring.config.location', System.properties['spring.config.location']
  if (project.hasProperty('jvmArgs')) {
    jvmArgs += project.jvmArgs.split("\\s+").toList()
  }
}

springBoot {
  mainClassName = 'com.sequenceiq.datalake.DatalakeApplication'
}

bootJar {
  baseName = 'datalake'
  archiveName = 'datalake.jar'
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
}

uploadBootArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: "$System.env.NEXUS_URL") {
        authentication(userName: "$System.env.NEXUS_USER", password: "$System.env.NEXUS_PASSWORD")
      }
    }
  }
}
