import org.ajoberstar.grgit.Grgit

buildscript {
  repositories {
    maven { url = "$repoUrl" }
    maven { url = "$springRepoUrl" }
    mavenCentral()
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}"
  }
}

plugins {
  id "java"
  id "checkstyle"
  id "jacoco"
  id "maven-publish"
  id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
  id "org.springframework.boot" version "${springBootVersion}"
  id "org.sonarqube" version "${sonarVersion}"
}

apply plugin: "org.sonarqube"

sonar {
  properties {
    property("sonar.projectName", "Datalake_Service_UT_master")
    property("sonar.projectKey", "Datalake_Service_UT_master")
    property("sonar.java.coveragePlugin", "plugin")
    property("sonar.sourceEncoding", "UTF-8")
    property("sonar.sources", "src/main")
    property("sonar.tests", "src/test")
    property("sonar.java.binaries", "build/classes")
    property("sonar.project.reporting.outputEncoding", "UTF-8")
    property("sonar.host.url", "$System.env.SONAR_URL")
    property("sonar.token", "$System.env.SONAR_TOKEN")
    property("org.gradle.internal.http.socketTimeout", "600000")
    property("org.gradle.internal.http.connectionTimeout", "600000")
    property("sonar.coverage.exclusions", "**/proto/**,**/generated/**")
    property("sonar.exclusions", "**/proto/**,**/generated/**")
  }
}

configurations {
  all {
    resolutionStrategy {
      force "org.yaml:snakeyaml:$snakeYamlVersion"
      force "io.netty:netty-codec:$nettyVersion"
    }
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.springframework', name: 'spring-webmvc', version: springFrameworkVersion
  }
}

dependencies {
  implementation project(':secret-rotation')
  implementation project(':secret-rotation-api')
  implementation project(':authorization-common')
  implementation project(':common')
  implementation project(':flow')
  implementation project(':secret-engine')
  implementation project(':notification-sender')
  implementation project(':idbmms-connector')
  implementation project(':datalake-dr-connector')
  implementation project(':sdx-connector')

  implementation project(':core-api')
  implementation project(':datalake-api')
  implementation project(':environment-api')
  implementation project(':redbeams-api')
  implementation project(':status-checker')

  implementation     project(":freeipa-api")
  implementation     project(':client-cm')
  implementation     project(":structuredevent-service-cdp")
  implementation     project(':environment-common')
  implementation     project(':service-common')
  implementation     project(':usage-collection')

  implementation     group: 'com.squareup.okhttp3',      name: 'okhttp',                          version: okhttpVersion
  implementation     group: 'com.google.code.gson',      name: 'gson',                            version: gsonVersion
  implementation     group: 'com.dyngr',                 name: 'polling',                         version: dyngrPollingVersion
  implementation     group: 'io.swagger.core.v3',        name: 'swagger-jaxrs2-jakarta',          version: swaggerCoreVersion
  implementation     group: 'javax.xml.bind',            name: 'jaxb-api',                        version: javaxXmlBindJaxbApiVersion
  implementation     group: 'org.apache.commons',        name: 'commons-lang3',                   version: apacheCommonsLangVersion
  implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server',                   version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.media',name: 'jersey-media-json-jackson',       version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.ext',  name: 'jersey-micrometer',               version: jerseyMicrometerVersion
  implementation     group: 'org.mybatis',               name: 'mybatis-migrations',              version: mybatisMigrationVersion
  implementation     group: 'org.postgresql',            name: 'postgresql',                      version: postgreSQLVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter',             version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web',         version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator',    version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey',      version: springBootVersion
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa',    version: springBootVersion

  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-quartz',      version: springBootVersion

  implementation     group: 'com.cloudera.api.swagger',    name: 'cloudera-manager-api-swagger',  version: cmClientVersion

  // mockito juniper depends on ByteBuddy 1.9.7, but hibernate use older version
  implementation group: 'net.bytebuddy',                 name: 'byte-buddy',                      version: byteBuddyVersion

  testImplementation project(path: ':secret-rotation', configuration: 'tests')

  testImplementation project(path: ':authorization-common', configuration: 'tests')
  testImplementation project(path: ':common', configuration: 'tests')
  testImplementation project(path: ':flow', configuration: 'tests')
  testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test',        version: springBootVersion
  testImplementation group: 'org.mockito',               name: 'mockito-inline',                  version: mockitoInlineVersion

  testRuntimeOnly group: 'org.testcontainers',      name: 'postgresql',                      version: testContainersVersion
  testRuntimeOnly group: 'org.awaitility',          name: 'awaitility',                      version: awaitilityVersion

}

bootRun {
  systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
  systemProperty 'spring.config.location', System.properties['spring.config.location']
  if (project.hasProperty('jvmArgs')) {
    jvmArgs += project.jvmArgs.split("\\s+").toList()
  }
}

bootRun {
  mainClass = 'com.sequenceiq.datalake.DatalakeApplication'
}

bootJar {
  archiveBaseName = 'datalake'
  archiveFileName= 'datalake.jar'
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
  //TODO  This could be removed if commercial side will be also on chainguard images
  requiresUnpack '**/bc*.jar'
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
    repositories {
        maven {
            url = "$System.env.NEXUS_URL"
            allowInsecureProtocol = true
            credentials {
                username = "$System.env.NEXUS_USER"
                password = "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}
