import org.ajoberstar.grgit.Grgit

buildscript {
  repositories {
    maven { url = "$repoUrl" }
    maven { url = "$springRepoUrl" }
    mavenCentral()
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}"
  }
}
plugins {
  id "java"
  id "checkstyle"
  id "jacoco"
  id "maven-publish"
  id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
  id "org.springframework.boot" version "${springBootVersion}"
  id "org.sonarqube" version "${sonarVersion}"
}

apply plugin: "org.sonarqube"

sonar {
  properties {
    property("sonar.projectName", "Redbeams_Service_UT_master")
    property("sonar.projectKey", "Redbeams_Service_UT_master")
    property("sonar.java.coveragePlugin", "plugin")
    property("sonar.sourceEncoding", "UTF-8")
    property("sonar.sources", "src/main")
    property("sonar.tests", "src/test")
    property("sonar.java.binaries", "build/classes")
    property("sonar.project.reporting.outputEncoding", "UTF-8")
    property("sonar.host.url", "$System.env.SONAR_URL")
    property("sonar.token", "$System.env.SONAR_TOKEN")
    property("org.gradle.internal.http.socketTimeout", "600000")
    property("org.gradle.internal.http.connectionTimeout", "600000")
    property("sonar.coverage.exclusions", "**/proto/**,**/generated/**")
    property("sonar.exclusions", "**/proto/**,**/generated/**")
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.springframework',      name: 'spring-webmvc',      version: springFrameworkVersion
    dependency group: 'io.projectreactor.netty',  name: 'reactor-netty-http', version: reactorNettyHttp
  }
}

dependencies {
  implementation     group: 'com.squareup.okhttp3',      name: 'okhttp',                     version: okhttpVersion
  implementation     group: 'io.swagger.core.v3',        name: 'swagger-jaxrs2-jakarta',     version: swaggerCoreVersion
  implementation     group: 'javax.xml.bind',            name: 'jaxb-api',                   version: javaxXmlBindJaxbApiVersion

  implementation     group: 'org.yaml',                  name: 'snakeyaml', version: snakeYamlVersion
  // mockito juniper depends on ByteBuddy 1.9.7, but hibernate use older version
  implementation            group: 'net.bytebuddy',             name: 'byte-buddy', version: byteBuddyVersion

  implementation     group: 'com.google.guava',          name: 'guava'
  implementation     group: 'com.google.code.gson',      name: 'gson',              version: gsonVersion
  implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server'
  implementation     group: 'org.mybatis',               name: 'mybatis-migrations'
  implementation     group: 'org.postgresql',            name: 'postgresql'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa'
  implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-quartz',           version: springBootVersion
  implementation     group: 'org.glassfish.jersey.ext',  name: 'jersey-micrometer',                    version: jerseyMicrometerVersion
  implementation     group: 'org.apache.commons',        name: 'commons-lang3',                        version: apacheCommonsLangVersion

  implementation(group: 'net.minidev', name: 'json-smart') {
    version {
      strictly jsonSmartVersion
    }
    because 'CVE-2021-31684'
  }
  implementation(group: 'net.minidev', name: 'accessors-smart') {
    version {
      strictly accessorSmartVersion
    }
    because 'because there is no jar for 2.4.3 in maven central repo...'
  }

  testImplementation ('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit'
  }
  testImplementation project(path: ':secret-rotation', configuration: 'tests')
  testImplementation project(path: ':authorization-common', configuration: 'tests')
  testImplementation group: 'org.mockito',               name: 'mockito-core'
  testImplementation project(path: ':common', configuration: 'tests')
  testImplementation project(path: ':flow', configuration: 'tests')
  testImplementation project(path: ':cloud-aws-common', configuration: 'tests')

  constraints {
    implementation     group: 'org.yaml',                  name: 'snakeyaml',                      version: snakeYamlVersion
    implementation     group: 'com.google.guava',          name: 'guava',                          version: guavaVersion
    implementation     group: 'com.google.code.gson',      name: 'gson',                           version: gsonVersion
    implementation     group: 'org.glassfish.jersey.core', name: 'jersey-server',                  version: jerseyCoreVersion
    implementation     group: 'org.mybatis',               name: 'mybatis-migrations',             version: mybatisMigrationVersion
    implementation     group: 'org.postgresql',            name: 'postgresql',                     version: postgreSQLVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter',            version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-web',        version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-actuator',   version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-jersey',     version: springBootVersion
    implementation     group: 'org.springframework.boot',  name: 'spring-boot-starter-data-jpa',   version: springBootVersion
    testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test',       version: springBootVersion
  }

  implementation project(':secret-rotation')
  implementation project(':secret-rotation-api')
  implementation project(':authorization-common')
  implementation project(':flow')
  implementation project(':cloud-reactor')
  implementation project(':cloud-reactor-api')
  implementation project(':environment-api')
  implementation project(':datalake-api')
  implementation project(':status-checker')
  implementation project(':cloud-common')
  implementation project(":cloud-azure")
  implementation project(':secret-engine')
  implementation project(':common')
  implementation project(':auth-connector')
  implementation project(':core-api')
  implementation project(':redbeams-api')
  implementation project(':template-manager-tag')
  implementation project(':structuredevent-service-cdp')
  implementation project(':service-common')

  runtimeOnly project(':cloud-gcp')
  runtimeOnly project(':cloud-aws-cloudformation')
  runtimeOnly project(':cloud-mock')
  runtimeOnly project(':cloud-azure')

  testRuntimeOnly group: 'org.testcontainers',        name: 'postgresql',                     version: testContainersVersion
  testRuntimeOnly group: 'org.awaitility',            name: 'awaitility',                     version: awaitilityVersion
}

bootRun {
  systemProperty "spring.profiles.active", System.properties["spring.profiles.active"]
  systemProperty "spring.config.location", System.properties["spring.config.location"]
  systemProperty 'spring.config.additional-location', System.properties['spring.config.additional-location']

  environment "AWS_ACCESS_KEY_ID", System.getenv('AWS_ACCESS_KEY_ID')
  environment "AWS_SECRET_ACCESS_KEY", System.getenv('AWS_SECRET_ACCESS_KEY')
  environment "CB_AWS_ACCOUNT_ID", System.getenv('CB_AWS_ACCOUNT_ID')
  environment "AWS_GOV_ACCESS_KEY_ID", System.getenv('AWS_GOV_ACCESS_KEY_ID')
  environment "AWS_GOV_SECRET_ACCESS_KEY", System.getenv('AWS_GOV_SECRET_ACCESS_KEY')
  environment "CB_AWS_GOV_ACCOUNT_ID", System.getenv('CB_AWS_GOV_ACCOUNT_ID')
  //Probably will need to add more for AZURE, GCE, etc

  if (project.hasProperty("jvmArgs")) {
    jvmArgs += project.jvmArgs.split("\\s+").toList()
  }
}

bootRun {
  mainClass = 'com.sequenceiq.redbeams.RedbeamsApplication'
}

bootJar {
  archiveBaseName = 'redbeams'
  archiveFileName= 'redbeams.jar'
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
  //TODO  This could be removed if commercial side will be also on chainguard images
  requiresUnpack '**/bc*.jar'
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
    repositories {
        maven {
            url = "$System.env.NEXUS_URL"
            allowInsecureProtocol = true
            credentials {
                username = "$System.env.NEXUS_USER"
                password = "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}

