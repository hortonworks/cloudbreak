buildscript {
  repositories {
    mavenCentral()
    maven { url = "$repoUrl" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}"
  }
}

plugins {
  id "java-library"
}

apply plugin: 'com.google.protobuf'

dependencies {
  api project(':grpc-common')
  implementation     project(":datalake-api")
  implementation     project(":flow-api")
  implementation     project(":auth-connector")
  implementation     project(':environment-api')
  implementation     project(':environment-remote-api')
  implementation     project(":environment-public-api-client")
  implementation     project(":servicediscovery-api-client")
  implementation     project(':service-common')

  implementation     group: 'org.springframework.boot',          name: 'spring-boot-starter-jersey',     version: springBootVersion

  implementation     group: 'org.glassfish.jersey.core',         name: 'jersey-client',                  version: jerseyCoreVersion
  implementation     group: 'org.glassfish.jersey.media',        name: 'jersey-media-multipart',         version: jerseyCoreVersion
  implementation     group: 'org.apache.httpcomponents.client5', name: 'httpclient5',                    version: httpClientVersion

  implementation     group: 'javax.annotation',                  name: 'javax.annotation-api',           version: javaxAnnotationApiVersion
  implementation     group: 'com.google.protobuf',               name: 'protobuf-java',                  version: protobufVersion
  implementation     group: 'com.dyngr',                         name: 'polling',                        version: dyngrPollingVersion
  implementation     (group: "com.cloudera.api.swagger",      name: "cloudera-manager-api-swagger",   version: cmClientVersion) {
    transitive = false
  }

  api ("io.grpc:grpc-all:$grpcVersion") {
    exclude group: 'org.mockito'
    exclude group: 'junit', module: 'junit'
  }
  testImplementation group: 'org.mockito',              name: 'mockito-core',                   version: mockitoVersion
  testImplementation group: 'org.assertj',              name: 'assertj-core',                   version: assertjVersion
  testImplementation group: 'org.hamcrest',             name: 'hamcrest',                       version: hamcrestVersion
  testImplementation group: 'net.bytebuddy',            name: 'byte-buddy',                     version: byteBuddyVersion
  testImplementation project(path: ':common', configuration: 'tests')

}


checkstyle {
  checkstyleMain.exclude '**/generated/**'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc { option '@generated=omit' }
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}
